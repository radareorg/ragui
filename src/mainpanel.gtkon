/* ragui - copyright(C) 2009-2011 - pancake<nopcode.org> */

VBox using=Gtk $Ragui.MainPanel {
	Label label="";
	HBox {
		Label label="";
		VBox {
			Label label="";
			Label !expand label="Editor";
			Button !expand label="Open file" activate=onOpenFile released=onOpenFile;
			Button !expand label="Open program" activate=onOpenProgram released=onOpenProgram;
			Button !expand label="Open project" activate=onOpenProject released=onOpenProject;
		}
		Label label="";
		VBox {
			Label label="";
			Label !expand label="Debugger";
			Button !expand label="Debug local program" activate=onDebugProgram released=onDebugProgram;
			Button !expand label="Attach to process" activate=onDebugAttach released=onDebugAttach;
			Button !expand label="Remote debugger" activate=onDebugRemote released=onDebugRemote;
		}
		Label label="";
	}
	Label label="";
	HBox {
		Label label="";
		VBox {
			Label !expand label="Diffing";
			Button !expand label="Diff files";
			Button !expand label="Diff programs";
			Button !expand label="Diff projects";
			Label label="";
		}
		Label label="";
		VBox {
			Label !expand label="Help";
			// lambda required to match signature
			Button !expand label="API documentation" released="{onHelpAPI();}";
			Button !expand label="About this program" released="{onHelpAbout();}";
			Label label="";
		}
		Label label="";
	}
	Label label="";
-{
	public signal void onHelpAPI ();
	public signal void onHelpAbout ();

	// TODO: centralize all open_file () calls here
	private void handle_open (Dialog w, FileChooser fc, bool bin) {
		int ret = 0;
		w.border_width = 6;
		do {
			w.show_all ();
			ret = w.run ();
		} while (ret == -1);
		w.hide ();
		w = null;
		if (ret == 1) {
			var filename = fc.get_filename ();
			if (!open_file (filename, bin)) {
				gc.show_error (@"Cannot open file $filename\n");
			}
		}
	}

	public void onOpenProgram () {
		var w = new Dialog.with_buttons ("Open program",
				gc.window, DialogFlags.MODAL,
				"autodetect", -1,
				"gtk-cancel", 0,
				"gtk-ok", 1
			);
		var op = new OpenProgram ();
		w.vbox.add (op);
		handle_open (w, op.fc, true);
		op.set_config ();
	}

	public void onOpenFile () {
		var w = new Dialog.with_buttons ("Open file",
				gc.window, DialogFlags.MODAL,
				"gtk-cancel", 0,
				"gtk-ok", 1
			);
		var of = new OpenFile ();
		w.vbox.add (of);
		w.resize (600, 400);
		handle_open (w, of.fc, false);
	}

	public void onOpenProject () {
		var w = new Dialog.with_buttons ("Open project",
				gc.window, DialogFlags.MODAL,
				"gtk-cancel", 0,
				"gtk-ok", 1
			);
		w.resize (600, 400);
		var prjdir = Environment.get_home_dir () + "/.radare2/rdb";
		var of = new OpenProject (prjdir);
		w.vbox.add (of);
		handle_open (w, of.fc, true);
	}

	public void onDebugProgram() {
		int ret = 0;
		var w = new Dialog.with_buttons ("Open program to debug",
				gc.window, DialogFlags.MODAL,
				"gtk-cancel", 0,
				"gtk-ok", 1
			);
		w.resize (600, 400);
		var of = new OpenProgram ();
		do {
			w.vbox.add (of);
			w.show_all ();
			ret = w.run ();
		} while (ret == -1);
		w.hide ();
		w = null;
		if (ret == 1) {
			// TODO: setup all that shit
			open_file ("dbg://"+of.fc.get_filename (), true);
		}
	}

	public void onDebugRemote() {
		int ret = 0;
		var w = new Dialog.with_buttons ("Open remote debug",
				gc.window, DialogFlags.MODAL,
				"gtk-cancel", 0,
				"gtk-ok", 1
			);
		w.resize (600, 400);
		var of = new OpenRemote ();
		do {
			w.vbox.add (of);
			w.show_all ();
			ret = w.run ();
		} while (ret == -1);
		w.hide ();
		w = null;
		if (ret == 1) {
			// TODO: setup all that shit
			open_file (of.get_url (), true);
		}
	}

	public void onDebugAttach() {
		int ret = 0;
		var w = new Dialog.with_buttons ("Attach to process",
				gc.window, DialogFlags.MODAL,
				"gtk-cancel", 0,
				"gtk-ok", 1
			);
		w.resize (600, 400);
		var of = new OpenProcess ();
		do {
			w.vbox.add (of);
			w.show_all ();
			ret = w.run ();
		} while (ret == -1);
		w.hide ();
		w = null;
		if (ret == 1) {
			// TODO: setup all that debug shit
			open_file ("attach://%d".printf (of.get_pid ()), false);
		}
	}

	public signal bool open_file(string file, bool analyze);
}-
}
