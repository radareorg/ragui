/* ragui - Copyright(C) 2009-2010 - pancake<nopcode.org> */

VBox gtkon:version=0.2 using=Gtk name=Ragui.Console {
	ScrolledWindow can-focus hscrollbar-policy="{PolicyType.AUTOMATIC}" {
		TextView can-focus !editable $.console;
	}
	HBox !expand {
		Entry expand $.line;
		Button label=">" !expand $.button;
	}
-{
	public signal void cmd_handler(string cmd);

	public Console() {
		button.released.connect (send_cmd);
		line.activate.connect (send_cmd);

		fixedtag = console.buffer.create_tag ("font", "font", "fixed", null);
	}

	private void send_cmd() {
		cmd_handler (line.text);
		line.set_text ("");
	}
	private TextTag fixedtag;

	public void set_text(string str) {
		Gtk.TextIter ei; 
		Gtk.TextMark im;
		console.buffer.get_end_iter (out ei); 
		//console.buffer.insert (ei, str, -1);
		console.buffer.insert_with_tags (ei, str, -1, fixedtag);
		/* Autoscroll */
		console.buffer.get_end_iter (out ei); 
		im = console.buffer.get_insert (); 
		console.buffer.place_cursor (ei); 
		console.scroll_to_mark (im, 0.0, true, 0.0, 1.0);
	}
}-
}
