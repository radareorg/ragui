/* ragui - copyright(C) 2009-2010 - pancake<nopcode.org>, nible<develsec.org> */

VBox using=Gtk $Ragui.MainBody border-width=1
	using:Radare="Radare"
	using:Ragui="Ragui"
	using:Grava="Grava"
	using:Grasmwidget="Grasmwidget"
	using:Codeview="Codeview"
	using:Hexview="Hexview"
	using:Listview="Listview"
{
/*
		Toolbar !expand {
			Button use-stock label="preferences";
			VSeparator;
			Button use-stock label="breakpoint";
			Button use-stock label="step";
			Button use-stock label="continue";
		}
*/
			HPaned $panel position=250 {
				VBox spacing=3 {
					ComboBox text !expand $leftbox;
					VBox $vb0 {
						ScrolledWindow can-focus expand
							hscrollbar-policy="{PolicyType.AUTOMATIC}"
							vscrollbar-policy="{PolicyType.AUTOMATIC}" {

							// TODO: move listview inside leftvb.. or we toggle visibility? do we need leftvb?
							Listview:Widget $listview;
						}
					}
				}

				VPaned $body position=500 add2 {
					VBox {
						// TODO: set each tab to a var, and use it from see code/graph
						Notebook expand scrollable tab-vborder=1 {
							VBox {
								tab-label {
									Label label="code/data";
								}
								VPaned $codhex position=300 {
									Codeview:Widget $codeview;
									Hexview:Widget $hexview add2;
								}
							}
							VBox {
								tab-label {
									Label label="graph";
								}
								VBox {
									Grava:Widget $grava;
								}
							}
							VBox $vbheaders {
								tab-label {
									Label label="Headers";
								}
							}
							/*Label label="hash-foo" {
							  tab-label {
							  Label label="data analysis";
							  }
							  }*/
						}
					}
					Ragui:Console $console add2;
				}
			}
/*
		HBox !expand {
			ProgressBar !expand;
			Statusbar expand;
		}
*/

	AboutDialog gtkaml:standalone $.aboutdialog delete-event="aboutdialog.hide_on_delete"
		border-width=5 title="About ragui" !resizable modal !has-separator
		window-position="{WindowPosition.CENTER_ON_PARENT}"
		program-name="ragui" version="0.1"
		copyright="Copyright (c) 2009-2010 pancake/nibble"
		comments="Graphical hexeditor, disassembler and debugger"
		website="http://www.radare.org/"
		authors='{new string[] {"pancake (pancake@nopcode.org)", "nibble (nibble@develsec.org)", null}}';

	-{
		public MainBody () {
			Grava.XDot.import (grava.graph, "widgets/grava/test.xdot");
			setup_console ();
			setup_leftbox ();
			setup_leftlist ();
			setup_io ();
			setup_headers ();
		}

		private void setup_leftbox () {
			leftbox.append_text ("Information");
			leftbox.append_text ("Sections");
			leftbox.append_text ("Imports");
			leftbox.append_text ("Symbols");
			leftbox.append_text ("Functions");
			leftbox.append_text ("Relocations");
			leftbox.append_text ("Entry Points");
			leftbox.append_text ("Strings");
			leftbox.append_text ("Registers");
			leftbox.append_text ("Breakpoints");
			leftbox.append_text ("Processes");
			leftbox.append_text ("Comments");
			leftbox.append_text ("Threads");
			leftbox.set_active (0);
			leftbox.changed.connect ( (x)=> {
				change_leftlist (x.get_active_text ());
			});
			change_leftlist ("Information");
		}

		public void setup_view () {
			if (this.get_has_window ()) {
				int h = 0, w = 0;
				this.window.get_size (out w, out h);
				body.position = h/2;
			}
		}

		private void setup_leftlist () {
			var lv = listview;
			lv.menu_construct.connect ((m) => {
				var cbtext = leftbox.get_active_text ();
				print (@"Choosen menu is $cbtext\n");
				switch (cbtext) {
				case "Information":
					listview.set_actions ("backup");
					break;
				case "Relocations":
					listview.set_actions ("see code", "see data");
					break;
				case "Breakpoints":
					listview.set_actions ("add", "remove", "enable", "disable");
					break;
				case "Sections":
					listview.set_actions ("see code", "see data", "resize", "delete", "dump", "restore", "change perms");
					break;
				case "Entry Points":
				case "Strings":
				case "Imports":
				case "Symbols":
					listview.set_actions ("disasm", "see data", "graph", "see code", "see data", "set breakpoint");
					break;
				case "Processes":
					listview.set_actions ("attach", "stop", "continue", "kill");
					break;
				case "Threads":
					listview.set_actions ("select", "pause", "continue", "kill");
					break;
				}
				//listview.set_actions ("seek", "breakpoint", "continue until", "inspect");
			});
			lv.menu_handler.connect ((m, d) => {
				//print ("m=%p\n", m);
				var action = m.to_string ();
				uint64 offset = d.offset;
				switch (action) {
				case "dump":
					var w = new DumpData ();
					w.transient_for = gc.window;
					// TODO: set size listw must store this too
					w.set_info (d.name, d.offset, d.offset);
					w.show_all ();
					break;
				case "backup":
					break;
				case "disasm":
					gc.seek (offset);
					gc.cmd ("ab");
					gc.cmd ("af");
					console.cmd (@"pdf");
					break;
				case "see code":
					gc.show_error ("Not yet implemented");
					break;
				case "see data":
					hexview.seek (offset);
					break;
				case "graph":
					gc.cmd (@"s $offset");
					gc.cmd ("pd 20");
					gc.cmd ("ab");
					gc.cmd ("af");
					gc.cmd (@"ag $offset > /tmp/graph.xdot");
					bool use_xdot = false;
					if (use_xdot) {
						RSystem.cmd ("xdot /tmp/graph.xdot &");
					} else {
						grava.graph.init ();
						Grava.XDot.import (grava.graph, "/tmp/graph.xdot");
						grava.graph.update ();
						grava.draw ();
					}
					break;
				// breakpoints
				case "set breakpoint":
					gc.cmd (@"db $offset");
					break;
				case "remove":
					gc.cmd (@"db-$offset");
					break;
				case "enable":
					gc.cmd (@"dbe @ $offset");
					break;
				case "disable":
					gc.cmd (@"dbd @ $offset");
					break;
				}
				print ("clicked "+m.to_string ()+": "+
					d.name+"at addr"+d.offset.to_string ()+"\n");
			});
		}

		private inline string S(string str) {
			if (str == null)
				return "";
			return str;
		}

		private void change_leftlist (string type) {
			var lv = listview;
			var baddr = gc.core.bin.get_baddr();
			lv.clear ();
			lv.show ();
			var info = gc.core.bin.get_info ();
			if (info == null)
				return;
			switch (type) {
			case "Information":
				if (info != null) {
					lv.add_row_s ("file", S(info.file));
					lv.add_row_s ("class", S(info.rclass));
					lv.add_row_s ("type", S(info.type));
					lv.add_row_s ("OS", S(info.os));
					lv.add_row_s ("machine", S(info.machine));
					lv.add_row_s ("subsystem", S(info.subsystem));
					lv.add_row_s ("bits", info.bits.to_string ());
					lv.add_row_s ("endian", info.big_endian?"big":"little");
				}
				// TODO lv.add_row_s ("relocs", info.dbg_info.is_stripped ()?"true":"false");
				break;
			case "Sections":
				foreach (var scn in gc.core.bin.get_sections ())
					lv.add_row (baddr+scn.rva, scn.name);
				break;
			case "Strings":
				foreach (var str in gc.core.bin.get_strings ())
					lv.add_row (baddr+str.rva, str.string);
				break;
			case "Imports":
				foreach (var imp in gc.core.bin.get_imports ())
					lv.add_row (baddr+imp.rva, imp.name);
				break;
			case "Symbols":
				foreach (var sym in gc.core.bin.get_symbols ())
					lv.add_row (baddr+sym.rva, sym.name);
				break;
			case "Relocations":
				foreach (var rel in gc.core.bin.get_relocs ())
					lv.add_row (baddr+rel.rva, rel.name);
				break;
			case "Entry Points":
				int i = 0;
				foreach (var entry in gc.core.bin.get_entries ())
					lv.add_row (baddr+entry.rva, "entry%i".printf (i++));
				break;
			case "Registers":
				break;
			}
		}

		private void setup_io () {
			hexview.seek (0);
			hexview.buffer.update.connect ((x, y)=> {
				print ("READING FROM 0x%08llx\n", x);
				hexview.buffer.start = x;
				hexview.buffer.end = x+y;
				hexview.buffer.size = y;
				hexview.buffer.bytes = new uint8[y];
				uint8 *ptr = hexview.buffer.bytes;
				gc.core.read_at (x, ptr, y);
			});
		}

		private void setup_console () {
			var cons = console;
			cons.cmd_handler.connect ((x) => {
				var prompt = ("[0x%08"+uint64.FORMAT_MODIFIER+"x] ").printf (gc.core.offset);
				var cmd = gc.core.cmd_str (x);
				cons.set_text (prompt+x+"\n"+cmd);
			});
		}

		private void setup_headers () {
			var grabin = new Grabin.All (gc.core.bin);
			grabin.sections.set_actions ("seek", "dump");
			grabin.sections.menu_handler.connect ((m, d) => {
					switch (d) {
					case "seek":
						//gc.seek ();
						gc.show_error ("Section dump not yet implemented");
						break;
					case "dump":
						gc.show_error ("Section dump not yet implemented");
						break;
					}
					print ("Sections clicked "+m+": "+d+"\n");
				});
			grabin.symbols.set_actions ("Seek");
			grabin.symbols.menu_handler.connect ((m, d) => {
					print ("Symbols clicked "+m.to_string ()+": "+d+"\n");
				});
			grabin.imports.set_actions ("Seek");
			grabin.imports.menu_handler.connect ((m, d) => {
					print ("Imports clicked "+m.to_string ()+": "+d+"\n");
				});
			grabin.strings.set_actions ("Seek");
			grabin.strings.set_retcolumn (Grabin.Strings.COLUMN.String);
			grabin.strings.menu_handler.connect ((m, d) => {
					print ("Strings clicked "+m.to_string ()+": "+d+"\n");
				});
			grabin.relocs.set_actions ("Seek");
			grabin.relocs.menu_handler.connect ((m, d) => {
					print ("Relocations clicked "+m.to_string ()+": "+d+"\n");
				});
			grabin.fat.set_actions ("Load", "Extract");
			grabin.fat.menu_handler.connect ((m, d) => {
					print ("SubBin clicked "+m.to_string ()+": "+d+"\n");
				});
			vbheaders.add (grabin);
		}

		public enum PanelType {
			LEFT,
			CODE,
			HEX,
			CONS,
			GRAPH,
			BIN
		}

		public void show_panel (PanelType pt) {
			switch (pt) {
			case PanelType.LEFT:
				if (panel.position == 300)
					panel.position = 0;
				else panel.position = 300;
				break;
			case PanelType.CODE:
				break;
			case PanelType.HEX:
				break;
			case PanelType.CONS:
				break;
			case PanelType.GRAPH:
				break;
			case PanelType.BIN:
				break;
			}
		}
	}-
}
